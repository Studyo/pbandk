import org.jetbrains.kotlin.gradle.plugin.*

// This build.gradle file is still using groovy because of a bug
// with Kotlin DSL and the npm plugin
// https://youtrack.jetbrains.com/issue/KT-34389

plugins {
    id "org.jetbrains.kotlin.plugin.serialization"
    id "maven-publish"
}

apply plugin: "org.jetbrains.kotlin.multiplatform"
apply plugin: 'kotlinx-serialization'

kotlin {

    jvm {
        withJava()
    }
    js {
        useCommonJs()

        browser {}

        nodejs {}
    }
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    // macosX64("macos")

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$kotlin_serialization_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin("stdlib")
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlin_serialization_version"
                implementation "com.google.protobuf:protobuf-java:$protobuf_version"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation "junit:junit:4.12"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin("stdlib-js")
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$kotlin_serialization_version"
                implementation npm("protobufjs", "^6.8.8")
            }
        }
        jsTest {
            dependencies {
                implementation kotlin("test-js")
            }
        }

        /*
        macosMain {
        }
        macosTest {
        }
        */
    }
}

apply from: rootProject.file('gradle/pom.gradle')

def withPom = { pom ->
    pom.withXml {
        def root = asNode()
        root.appendNode('name', project.name)
        root.appendNode('description', "Library for pbandk runtime protobuf code")
        root.appendNode('url', "https://github.com/streem/pbandk")
        root.children().last() + pomConfig
    }
}

publishing {
    publications {
        mavenProject(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            withPom(pom)
        }
    }
}
