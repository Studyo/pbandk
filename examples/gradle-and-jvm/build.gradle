buildscript {
    ext.kotlin_serialization_version = "0.20.0"
    ext.kotlin_version = "1.3.71"
    ext.pbandk_version = "0.9.0-SNAPSHOT"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

apply plugin: "kotlin"
apply plugin: "kotlinx-serialization"
apply plugin: "application"
apply plugin: "com.google.protobuf"

mainClassName = "pbandk.examples.addressbook.MainKt"
applicationName = "addressbook"

dependencies {
    compile "com.github.streem.pbandk:pbandk-runtime-jvm:$pbandk_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlin_serialization_version"
}

compileJava.enabled = false

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.1"
    }
    plugins {
        kotlin {
            artifact = "com.github.streem.pbandk:protoc-gen-kotlin-jvm:$pbandk_version:jvm8@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                kotlin {
                    option "kotlin_package=pbandk.examples.addressbook.pb"
                }
            }
        }
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
