buildscript {
    ext.kotlin_serialization_version = "0.20.0"
    ext.kotlin_version = "1.3.71"
    ext.pbandk_version = "0.9.0-SNAPSHOT"
    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"
        classpath "org.jetbrains.kotlin:kotlin-frontend-plugin:0.0.45"
    }
}

apply plugin: "kotlin2js"
apply plugin: "kotlin-dce-js"
apply plugin: "kotlinx-serialization"
apply plugin: "org.jetbrains.kotlin.frontend"
apply plugin: "com.google.protobuf"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // This is needed for includes, ref: https://github.com/google/protobuf-gradle-plugin/issues/41#issuecomment-143884188
    compileOnly "com.google.protobuf:protobuf-java:3.11.1"
    compile "com.github.streem.pbandk:pbandk-runtime-js:$pbandk_version"

    implementation "com.github.streem.pbandk:protoc-gen-kotlin-jvm:$pbandk_version:jvm8"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$kotlin_serialization_version"
}

compileJava.enabled = false

compileKotlin2Js {
    // Generate the protobuf files before compilation
    dependsOn "generateProto"
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = "commonjs"
}

kotlinFrontend {
    downloadNodeJsVersion = "latest"

    npm {
        dependency("protobufjs", "^6.8.8")
    }

    webpackBundle {
        bundleName = "main"
        contentPath = file("src/main/web")
        mode = "production"
    }
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"
    protoc {
        artifact = "com.google.protobuf:protoc:3.11.1"
    }
    plugins {
        kotlin {
            artifact = "com.github.streem.pbandk:protoc-gen-kotlin-jvm:$pbandk_version:jvm8@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                kotlin {
                    option "kotlin_package=pbandk.examples.browserjs.pb"
                }
            }
        }
    }
}
