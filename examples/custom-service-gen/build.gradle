buildscript {
    ext.kotlin_version = "1.3.71"
    ext.pbandk_version = "0.9.0-SNAPSHOT"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.12"
        classpath "com.android.tools.build:gradle:2.3.0"
    }
}

allprojects {
    apply plugin: "kotlin"

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

project(":application") {
    apply plugin: "application"
    apply plugin: "com.google.protobuf"

    mainClassName = "pbandk.examples.greeter.MainKt"
    applicationName = "greeter"

    dependencies {
        compile "com.github.streem.pbandk:pbandk-runtime-jvm:$pbandk_version"
        implementation "com.github.streem.pbandk:protoc-gen-kotlin-jvm:$pbandk_version"
        compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6"
    }

    compileJava.enabled = false

    protobuf {
        generatedFilesBaseDir = "$projectDir/src"
        protoc {
            artifact = "com.google.protobuf:protoc:3.11.1"
        }
        plugins {
            kotlin {
                artifact = "com.github.streem.pbandk:protoc-gen-kotlin-jvm:$pbandk_version:jvm8@jar"
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.dependsOn ":generator:jar"
                task.builtins {
                    remove java
                }
                task.plugins {
                    kotlin {
                        option "log=debug"
                        option "kotlin_package=pbandk.examples.greeter.pb"
                        option "kotlin_service_gen=${project(":generator").buildDir}/libs/generator.jar|pbandk.examples.greeter.Generator"
                    }
                }
            }
        }
    }
}

project(":generator") {
    dependencies {
        compileOnly "com.github.streem.pbandk:pbandk-runtime-jvm:$pbandk_version"
        compileOnly "com.github.streem.pbandk:protoc-gen-kotlin-jvm:$pbandk_version"
    }
}
repositories {
    mavenCentral()
}
dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
